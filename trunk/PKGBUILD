# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Marius Knaust <marius.knaust@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>

pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.76.0
_boostver=${pkgver//./_}
pkgrel=1
pkgdesc='Free peer-reviewed portable C++ source libraries'
url='https://www.boost.org/'
arch=('x86_64')
license=('custom')
makedepends=('icu' 'python' 'python-numpy' 'bzip2' 'zlib' 'openmpi' 'zstd' 'findutils' 'boost.build')
source=(https://boostorg.jfrog.io/artifactory/main/release/${pkgver}/source/boost_${_boostver}.tar.bz2
        boost-1.76.0-no-sourcedir.patch)
sha256sums=('f0397ba6e982c4450f27bf32a2a83292aba035b827a5623a14636ea583318c41'
            '3cfd08adfde94be947452c7d9e350553f57692f435a89ac3dac90e38257fc909')
b2sums=('45445e6a9725cb99131e0b831b2fac0840d083692c13887b41adeac5cb8b3732026db3641d6be20591a676b78a87fcf363eb9b1508f87ed26039bba6a1ced533'
        '5e1221903960fe4e89bef0f3708167ec7630b0ac3703a7a534b33fac57b734ee8b49ee4c046221db61a13ae1d24ad4c6d20e95bfdfda32bb87cf200914ba15c3')

prepare() {
   cd ${pkgbase}_${_boostver}
   patch -p1 -i ../boost-1.76.0-no-sourcedir.patch
   rm boost-build.jam #This adds in local jamfiles which are used in preference to the system supplied ones which has been patched to apply $LDFLAGS earlier
}

build() {
   export _stagedir="${srcdir}/stagedir"
   local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

   cd ${pkgbase}_${_boostver}

   ./bootstrap.sh \
     --with-toolset=gcc \
     --with-icu \
     --with-python=/usr/bin/python3  \
     --with-bjam=b2

   # Support for OpenMPI
   echo "using mpi ;" >> project-config.jam

   # boostbook is needed by quickbook
   install -dm755 "${_stagedir}"/share/boostbook
   cp -a tools/boostbook/{xsl,dtd} "${_stagedir}"/share/boostbook/

   # default "minimal" install: "release link=shared,static
   # runtime-link=shared threading=single,multi"
   # --layout=tagged will add the "-mt" suffix for multithreaded libraries
   # and installs includes in /usr/include/boost.
   # --layout=system no longer adds the -mt suffix for multi-threaded libs.
   # install to ${_stagedir} in preparation for split packaging
   b2 \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared,static \
      toolset=gcc \
      python=3.9 \
      cflags="${CPPFLAGS} ${CFLAGS} -fPIC -O3" \
      cxxflags="${CPPFLAGS} ${CXXFLAGS} -std=c++17 -fPIC -O3" \
      linkflags="${LDFLAGS}" \
      --layout=system \
      ${JOBS} \
      \
      --prefix="${_stagedir}" \
      install
}

package_boost() {
   pkgdesc+=' - development headers'
   depends=("boost-libs=${pkgver}")
   optdepends=('python: for python bindings')
   options=('staticlibs')

   install -dm755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/{include,share} "${pkgdir}"/usr

   install -d "${pkgdir}"/usr/lib
   cp -a "${_stagedir}"/lib/*.a "${pkgdir}"/usr/lib/
   cp -a "${_stagedir}"/lib/cmake "${pkgdir}"/usr/lib/

   install -Dm644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt
}

package_boost-libs() {
   pkgdesc+=' - runtime libraries'
   depends=('bzip2' 'zlib' 'icu' 'zstd')
   optdepends=('openmpi: for mpi support')
   provides=(libboost_atomic.so libboost_chrono.so libboost_container.so
     libboost_context.so libboost_contract.so libboost_coroutine.so
     libboost_date_time.so libboost_fiber.so libboost_filesystem.so
     libboost_graph.so libboost_graph_parallel.so libboost_iostreams.so
     libboost_locale.so libboost_log.so libboost_log_setup.so
     libboost_math_c99.so libboost_math_c99f.so libboost_math_c99l.so
     libboost_math_tr1.so libboost_math_tr1f.so libboost_math_tr1l.so
     libboost_mpi.so libboost_numpy39.so
     libboost_prg_exec_monitor.so libboost_program_options.so
     libboost_python39.so libboost_random.so
     libboost_regex.so libboost_serialization.so
     libboost_stacktrace_addr2line.so libboost_stacktrace_basic.so
     libboost_stacktrace_noop.so libboost_system.so libboost_thread.so
     libboost_timer.so libboost_type_erasure.so libboost_unit_test_framework.so
     libboost_wave.so libboost_wserialization.so)

   install -dm755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/lib "${pkgdir}"/usr
   rm "${pkgdir}"/usr/lib/*.a
   rm -r "${pkgdir}"/usr/lib/cmake

   # https://github.com/boostorg/mpi/issues/112
   install -d "${pkgdir}/usr/lib/python3.9/site-packages/boost"
   touch "${pkgdir}/usr/lib/python3.9/site-packages/boost/__init__.py"
   python -m compileall -o 0 -o 1 -o 2 "${pkgdir}/usr/lib/python3.9/site-packages/boost"
   mv "${pkgdir}"/usr/lib/boost-python3.9/mpi.so \
      "${pkgdir}/usr/lib/python3.9/site-packages/boost/mpi.so"

   # https://github.com/boostorg/python/issues/203#issuecomment-391477685
   for _lib in python numpy; do
     ln -srL "${pkgdir}"/usr/lib/libboost_${_lib}3{9,}.so
   done

   install -Dm644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}

# vim: ts=2 sw=2 et:
